{.experimental: "strictDefs".}
{.experimental: "strictFuncs".}
{.experimental: "views".}

import std/[sequtils, strutils, sugar, unittest]
import ../../src/pon2/private/core/field/binary/[common]
import ../../src/pon2/core/[common, rule]

proc toBinField(str: string): BinField =
  ## Returns the binary field converted from the string representation.
  let strs = str.split "\n"
  require strs.len == Height
  require strs.allIt it.len == Width

  var arr: array[Row, array[Col, bool]]
  for row in Row:
    for col in Col:
      arr[row][col] = strs[row.ord][col.ord] == 'x'

  {.push warning[Uninit]: off.}
  return arr.toBinField
  {.pop.}

# ------------------------------------------------
# Constructor
# ------------------------------------------------

block: # initZero
  check BinField.initZero ==
    """
......
......
......
......
......
......
......
......
......
......
......
......
......""".toBinField

block: # initFloor
  check BinField.initFloor ==
    """
......
......
......
......
......
......
......
......
......
......
......
......
xxxxxx""".toBinField.shiftedDownRaw

block: # initLowerAir
  check BinField.initLowerAir ==
    """
......
......
......
......
xxxxxx
......
......
......
......
......
......
......
......""".toBinField

block: # initUpperWater
  check BinField.initUpperWater ==
    """
......
......
......
......
......
xxxxxx
......
......
......
......
......
......
......""".toBinField

# ------------------------------------------------
# Operator
# ------------------------------------------------

block: # `+`, `-`, `*`, `xor`, `+=`, `-=`, `*=`, sum, prod
  let
    f1 =
      """
xxxxxx
xxxxx.
xxxx..
xxx...
xx....
x.....
......
......
......
......
......
......
......""".toBinField
    f2 =
      """
x.....
xx....
xxx...
xxxx..
xxxxx.
xxxxxx
......
......
......
......
......
......
......""".toBinField
    fSum =
      """
xxxxxx
xxxxx.
xxxx..
xxxx..
xxxxx.
xxxxxx
......
......
......
......
......
......
......""".toBinField
    fDiff =
      """
.xxxxx
..xxx.
...x..
......
......
......
......
......
......
......
......
......
......""".toBinField
    fProd =
      """
x.....
xx....
xxx...
xxx...
xx....
x.....
......
......
......
......
......
......
......""".toBinField
    fXor =
      """
.xxxxx
..xxx.
...x..
...x..
..xxx.
.xxxxx
......
......
......
......
......
......
......""".toBinField

  check f1 + f2 == fSum
  check f1 - f2 == fDiff
  check f1 * f2 == fProd
  check (f1 xor f2) == fXor

  var f3 = f1
  f3 += f2
  check f3 == fSum

  f3 = f1
  f3 -= f2
  check f3 == fDiff

  f3 = f1
  f3 *= f2
  check f3 == fProd

  check sum(f1, f2, fProd) == f1 + f2 + fProd
  check sum(f1, f2, fProd, fDiff) == f1 + f2 + fProd + fDiff
  check sum(f1, f2, fProd, fDiff, fXor) == f1 + f2 + fProd + fDiff + fXor
  check sum(f1, f2, fProd, fDiff, fXor, f3) == f1 + f2 + fProd + fDiff + fXor + f3
  check sum(f1, f2, fProd, fDiff, fXor, f3, BinField.initZero()) ==
    f1 + f2 + fProd + fDiff + fXor + f3 + BinField.initZero()
  check sum(f1, f2, fProd, fDiff, fXor, f3, BinField.initZero(), BinField.initFloor()) ==
    f1 + f2 + fProd + fDiff + fXor + f3 + BinField.initZero() + BinField.initFloor()

  check prod(f1, f2, fXor) == f1 * f2 * fXor

# ------------------------------------------------
# Keep
# ------------------------------------------------

block: # kept, keptValid, keptVisible, keptAir, keepValid
  let field =
    """
x..x..
.x..x.
..x..x
x..x..
.x..x.
..x..x
......
......
......
......
......
......
......""".toBinField

  check field.kept(Row1) ==
    """
......
.x..x.
......
......
......
......
......
......
......
......
......
......
......""".toBinField
  check field.kept(Col5) ==
    """
......
......
.....x
......
......
.....x
......
......
......
......
......
......
......""".toBinField
  check BinField.initOne.keptValid ==
    """
xxxxxx
xxxxxx
xxxxxx
xxxxxx
xxxxxx
xxxxxx
xxxxxx
xxxxxx
xxxxxx
xxxxxx
xxxxxx
xxxxxx
xxxxxx""".toBinField
  check field.keptVisible ==
    """
......
.x..x.
..x..x
x..x..
.x..x.
..x..x
......
......
......
......
......
......
......""".toBinField
  check field.keptAir ==
    """
x..x..
.x..x.
..x..x
x..x..
.x..x.
......
......
......
......
......
......
......
......""".toBinField

  check BinField.initOne.dup(keepValid) == BinField.initOne.keptValid

# ------------------------------------------------
# Clear
# ------------------------------------------------

block: # clear
  check BinField.initOne.dup(clear) == BinField.initZero

  var field =
    """
x.....
.x....
..x...
...x..
....x.
.....x
......
......
......
......
......
......
......""".toBinField
  field.clear Col2
  check field ==
    """
x.....
.x....
......
...x..
....x.
.....x
......
......
......
......
......
......
......""".toBinField

# ------------------------------------------------
# Population Count
# ------------------------------------------------

block: # cntOne
  check BinField.initOne.keptValid.popcnt == Height * Width
  check BinField.initZero.popcnt == 0
  check BinField.initFloor.popcnt == Width

# ------------------------------------------------
# Shift
# ------------------------------------------------

block:
  # shiftedUpRaw, shiftedDownRaw, shiftedRightRaw, shiftedLeftRaw,
  # shiftUpRaw, shiftDownRaw, shiftRightRaw, shiftLeftRaw,
  # shiftedUp, shiftedDown, shiftedRight, shiftedLeft,
  # shiftUp, shiftDown, shiftRight, shiftLeft
  let
    field =
      """
x....x
.x...x
......
......
......
......
......
......
......
......
......
.....x
xx..x.""".toBinField
    fieldU =
      """
.x...x
......
......
......
......
......
......
......
......
......
.....x
xx..x.
......""".toBinField
    fieldD =
      """
......
x....x
.x...x
......
......
......
......
......
......
......
......
......
.....x""".toBinField
    fieldR =
      """
.x....
..x...
......
......
......
......
......
......
......
......
......
......
.xx..x""".toBinField
    fieldL =
      """
....x.
x...x.
......
......
......
......
......
......
......
......
......
....x.
x..x..""".toBinField

  check field.shiftedUpRaw.keptValid == fieldU
  check field.shiftedDownRaw.keptValid == fieldD
  check field.shiftedRightRaw.keptValid == fieldR
  check field.shiftedLeftRaw.keptValid == fieldL

  check field.dup(shiftUpRaw) == field.shiftedUpRaw
  check field.dup(shiftDownRaw) == field.shiftedDownRaw
  check field.dup(shiftRightRaw) == field.shiftedRightRaw
  check field.dup(shiftLeftRaw) == field.shiftedLeftRaw

  check field.shiftedUp == fieldU
  check field.shiftedDown == fieldD
  check field.shiftedRight == fieldR
  check field.shiftedLeft == fieldL

  check field.dup(shiftUp) == field.shiftedUp
  check field.dup(shiftDown) == field.shiftedDown
  check field.dup(shiftRight) == field.shiftedRight
  check field.dup(shiftLeft) == field.shiftedLeft

# ------------------------------------------------
# Flip
# ------------------------------------------------

block: # flippedVertical, flippedHorizontal, flipVertical, flipHorizontal
  let
    field =
      """
......
......
......
......
...x..
..x...
......
......
......
......
......
......
......""".toBinField
    fieldV =
      """
......
......
......
......
......
......
......
..x...
...x..
......
......
......
......""".toBinField
    fieldH =
      """
......
......
......
......
..x...
...x..
......
......
......
......
......
......
......""".toBinField

  check field.flippedVertical == fieldV
  check field.flippedHorizontal == fieldH

  check field.dup(flipVertical) == fieldV
  check field.dup(flipHorizontal) == fieldH

# ------------------------------------------------
# Indexer
# ------------------------------------------------

block: # `[]`, `[]=`
  var field =
    """
......
......
.x....
......
......
......
......
......
......
......
......
......
......""".toBinField

  check field[Row2, Col1]
  check not field[Row1, Col2]

  let
    row = Row1
    col = Col2
  check not field[row, col]

  field[row, col] = true
  check field[row, col]

  field[row, col] = false
  check not field[row, col]

# ------------------------------------------------
# Insert / Delete
# ------------------------------------------------

block: # insert, delete
  var field =
    """
......
.x....
.x....
x.....
....x.
......
......
......
......
......
......
x.....
x.....""".toBinField

  field.insert Row3, Col1, true, Tsu
  check field ==
    """
.x....
.x....
......
xx....
....x.
......
......
......
......
......
......
x.....
x.....""".toBinField

  field.insert Row4, Col1, false, Tsu
  check field ==
    """
.x....
......
.x....
x.....
....x.
......
......
......
......
......
......
x.....
x.....""".toBinField

  field.delete Row5, Col1, Tsu
  check field ==
    """
......
.x....
......
xx....
....x.
......
......
......
......
......
......
x.....
x.....""".toBinField

  field.insert Row4, Col0, true, Water
  check field ==
    """
......
.x....
x.....
.x....
x...x.
......
......
......
......
......
......
x.....
x.....""".toBinField

  field.insert Row5, Col0, false, Water
  check field ==
    """
......
.x....
x.....
.x....
x...x.
......
......
......
......
......
......
......
x.....""".toBinField

  field.delete Row4, Col0, Water
  check field ==
    """
......
.x....
......
xx....
....x.
......
......
......
......
......
......
......
x.....""".toBinField

  field.delete Row5, Col0, Water
  check field ==
    """
......
.x....
......
xx....
....x.
......
......
......
......
......
......
x.....
......""".toBinField

# ------------------------------------------------
# Drop
# ------------------------------------------------

block: # drop
  let field =
    """
xx...x
.xx...
.x.xx.
xx.xx.
xxx...
.xxx..
.x.xx.
xx.xx.
xxxxx.
.x.xx.
.x.xx.
.x.xx.
xx.xxx""".toBinField

  check field.dup(drop(_, field.toDropMask)) ==
    """
.x....
.x....
.x....
.x.x..
.x.xx.
.x.xx.
.x.xx.
xx.xx.
xx.xx.
xxxxx.
xxxxx.
xxxxxx
xxxxxx""".toBinField
