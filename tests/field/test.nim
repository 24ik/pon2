{.push raises: [].}
{.experimental: "strictDefs".}
{.experimental: "strictFuncs".}
{.experimental: "views".}

import std/[sequtils, sugar, unittest]
import
  ../../src/pon2/core/
    [cell, common, field, fqdn, moveresult, pair, placement, popresult, rule, step]
import ../../src/pon2/private/[assign3, arrayops2, strutils2]
import ../../src/pon2/private/core/[binfield]

proc toBinField(str: string): BinField =
  ## Returns the binary field converted from the string representation.
  let strs = str.split "\n"

  var arr {.noinit.}: array[Row, array[Col, bool]]
  for row in Row:
    for col in Col:
      arr[row][col] = strs[row.ord][col.ord] == 'x'

  return arr.toBinField

func toTsuField(str: string): TsuField =
  ## Returns the Tsu field converted from the string representation.
  str.parseTsuField.unsafeValue

func toWaterField(str: string): WaterField =
  ## Returns the Water field converted from the string representation.
  str.parseWaterField.unsafeValue

# ------------------------------------------------
# Constructor
# ------------------------------------------------

block: # init
  check TsuField.init ==
    """
......
......
......
......
......
......
......
......
......
......
......
......
......""".toTsuField

  check WaterField.init ==
    """
......
......
......
......
......
~~~~~~
......
......
......
......
......
......
......
......""".toWaterField

# ------------------------------------------------
# Convert
# ------------------------------------------------

block: # toTsuField, toWaterField
  let
    fieldT =
      """
r....g
......
......
......
......
......
......
......
......
......
......
......
o....h""".toTsuField
    fieldW =
      """
r....g
......
......
......
......
~~~~~~
......
......
......
......
......
......
......
o....h""".toWaterField

  check fieldT.toTsuField == fieldT
  check fieldW.toTsuField == fieldT

  check fieldT.toWaterField == fieldW
  check fieldW.toWaterField == fieldW

# ------------------------------------------------
# Property
# ------------------------------------------------

block: # rule
  check TsuField.init.rule == Tsu
  check WaterField.init.rule == Water

block: # isDead
  block: # Tsu
    check not """
oooooo
oo.ooo
oooooo
oooooo
oooooo
oooooo
oooooo
oooooo
oooooo
oooooo
oooooo
oooooo
oooooo""".toTsuField.isDead

    check """
......
..o...
......
......
......
......
......
......
......
......
......
......
......""".toTsuField.isDead

  block: # Water
    check not """
hhhhhh
hhhhhh
hhhhhh
hhhhhh
......
~~~~~~
hhhhhh
hhhhhh
hhhhhh
hhhhhh
hhhhhh
hhhhhh
hhhhhh
hhhhhh""".toWaterField.isDead

    check """
......
......
......
......
.....h
~~~~~~
......
......
......
......
......
......
......
......""".toWaterField.isDead

# ------------------------------------------------
# Placement
# ------------------------------------------------

block: # invalidPlacements, validPlacements, validDblPlacements
  block: # Tsu
    block:
      let field = TsuField.init
      check field.invalidPlacements == {}
      check field.validPlacements == {Placement.low .. Placement.high}
      check field.validDblPlacements == DblPlacements

    block:
      let field =
        """
......
.r....
......
......
......
......
......
......
......
......
......
......
......""".toTsuField
      check field.invalidPlacements ==
        {Up0, Right0, Down0, Up1, Right1, Down1, Left1, Left2}
      check field.validPlacements == {
        Up2, Up3, Up4, Up5, Right2, Right3, Right4, Down2, Down3, Down4, Down5, Left3,
        Left4, Left5,
      }
      check field.validDblPlacements == {Up2, Up3, Up4, Up5, Right2, Right3, Right4}

    block:
      let field =
        """
......
.r....
.....o
......
......
......
......
......
......
......
......
......
......""".toTsuField
      check field.invalidPlacements == {Down1}

    block:
      let field =
        """
......
.y.h..
......
......
......
......
......
......
......
......
......
......
......""".toTsuField
      check field.invalidPlacements == {Down1, Down3}

    block:
      let field =
        """
.r....
.g.b..
......
......
......
......
......
......
......
......
......
......
......""".toTsuField
      check field.invalidPlacements ==
        {Up0, Right0, Down0, Up1, Right1, Down1, Left1, Left2, Down3}

    block:
      let field =
        """
....h.
.p.o..
..r...
......
......
......
......
......
......
......
......
......
......""".toTsuField
      check field.invalidPlacements ==
        {Down1, Right3, Down3, Up4, Right4, Down4, Left4, Up5, Down5, Left5}

  block: # Water
    block:
      let field = WaterField.init
      check field.invalidPlacements == {}
      check field.validPlacements == {Placement.low .. Placement.high}
      check field.validDblPlacements == DblPlacements

    block:
      let field =
        """
......
.r....
......
......
......
~~~~~~
......
......
......
......
......
......
......
......""".toWaterField
      check field.invalidPlacements ==
        {Up0, Right0, Down0, Up1, Right1, Down1, Left1, Left2}
      check field.validPlacements == {
        Up2, Up3, Up4, Up5, Right2, Right3, Right4, Down2, Down3, Down4, Down5, Left3,
        Left4, Left5,
      }
      check field.validDblPlacements == {Up2, Up3, Up4, Up5, Right2, Right3, Right4}

    block:
      let field =
        """
......
.h....
.....o
......
......
~~~~~~
......
......
......
......
......
......
......
......""".toWaterField
      check field.invalidPlacements == {Down1}

    block:
      let field =
        """
......
.y.p..
......
......
......
~~~~~~
......
......
......
......
......
......
......
......""".toWaterField
      check field.invalidPlacements == {Down1, Down3}

    block:
      let field =
        """
.h....
.g.b..
......
......
......
~~~~~~
......
......
......
......
......
......
......
......""".toWaterField
      check field.invalidPlacements ==
        {Up0, Right0, Down0, Up1, Right1, Down1, Left1, Left2, Down3}

    block:
      let field =
        """
....y.
.p.o..
..r...
......
......
~~~~~~
......
......
......
......
......
......
......
......""".toWaterField
      check field.invalidPlacements ==
        {Down1, Right3, Down3, Up4, Right4, Down4, Left4, Up5, Down5, Left5}

# ------------------------------------------------
# Indexer
# ------------------------------------------------

block: # `[]`, `[]=`
  var
    fieldT =
      """
......
.r....
...o..
......
......
......
......
......
......
......
......
......
......""".toTsuField
    fieldW =
      """
....b.
......
......
......
......
~~~~~~
h.....
......
......
......
......
......
......
......""".toWaterField

  check fieldT[Row1, Col1] == Red
  check fieldT[Row0, Col0] == None
  check fieldW[Row5, Col0] == Hard
  check fieldW[Row1, Col1] == None

  fieldT[Row2, Col3] = Purple
  check fieldT[Row2, Col3] == Purple

  fieldW[Row0, Col0] = Yellow
  check fieldW[Row0, Col0] == Yellow

# ------------------------------------------------
# Insert / Delete
# ------------------------------------------------

block: # insert, delete
  block: # Tsu
    var field =
      """
g.....
g.....
.b....
......
..r.y.
...ph.
......
......
......
p.....
......
.o....
.o....""".toTsuField

    field.insert Row3, Col0, Red
    check field ==
      """
g.....
......
.b....
r.....
..r.y.
...ph.
......
......
......
p.....
......
.o....
.o....""".toTsuField

    field.insert Row0, Col5, Green
    check field ==
      """
g....g
......
.b....
r.....
..r.y.
...ph.
......
......
......
p.....
......
.o....
.o....""".toTsuField

    field.delete Row9, Col0
    check field ==
      """
.....g
g.....
.b....
......
r.r.y.
...ph.
......
......
......
......
......
.o....
.o....""".toTsuField

  block: # Water
    var field =
      """
g.....
g.....
.b....
......
..r.y.
~~~~~~
...ph.
......
......
......
p.....
......
.o....
.o....""".toWaterField

    field.insert Row4, Col0, Red
    check field ==
      """
g.....
......
.b....
......
r.r.y.
~~~~~~
...ph.
......
......
......
p.....
......
.o....
.o....""".toWaterField

    field.insert Row5, Col0, Green
    check field ==
      """
g.....
......
.b....
......
r.r.y.
~~~~~~
g..ph.
......
......
......
......
p.....
.o....
.o....""".toWaterField

    field.insert Row9, Col1, Yellow
    check field ==
      """
g.....
......
.b....
......
r.r.y.
~~~~~~
g..ph.
......
......
......
.y....
p.....
......
.o....""".toWaterField

    field.insert Row4, Col4, None
    check field ==
      """
g.....
......
.b....
....y.
r.r...
~~~~~~
g..ph.
......
......
......
.y....
p.....
......
.o....""".toWaterField

    field.insert Row8, Col5, Garbage
    check field ==
      """
g.....
......
.b....
....y.
r.r...
~~~~~~
g..ph.
......
......
.....o
.y....
p.....
......
.o....""".toWaterField

    field.delete Row4, Col0
    check field ==
      """
......
g.....
.b....
....y.
..r...
~~~~~~
g..ph.
......
......
.....o
.y....
p.....
......
.o....""".toWaterField

    field.delete Row5, Col0
    check field ==
      """
......
g.....
.b....
....y.
..r...
~~~~~~
...ph.
......
......
.....o
py....
......
......
.o....""".toWaterField

    field.delete Row12, Col5
    check field ==
      """
......
g.....
.b....
....y.
..r...
~~~~~~
...ph.
......
......
.....o
py....
......
......
.o....""".toWaterField

# ------------------------------------------------
# Count
# ------------------------------------------------

block: # cellCnt, puyoCnt, colorPuyoCnt, garbagesCnt
  let
    fieldT =
      """
......
......
rr....
.r..gg
....gg
......
..o...
..oo..
...o..
h..o..
......
......
......""".toTsuField
    fieldW =
      """
......
......
pp....
.p..gg
....gg
~~~~~~
......
..o...
..oo..
...o..
h..o..
......
......
......""".toWaterField

  check fieldT.cellCnt(Red) == 3
  check fieldT.cellCnt(Blue) == 0
  check fieldW.cellCnt(Garbage) == 5
  check fieldW.cellCnt(Red) == 0

  check fieldT.puyoCnt == 13
  check fieldW.puyoCnt == 13

  check fieldT.colorPuyoCnt == 7
  check fieldW.colorPuyoCnt == 7

  check fieldT.garbagesCnt == 6
  check fieldW.garbagesCnt == 6

# ------------------------------------------------
# Connect - 2
# ------------------------------------------------

block: # conn2, conn2Vertical, conn2Horizontal
  let
    fieldT =
      """
bbbrry
yggbgy
ygbbgg
yrbyyy
bbrggy
ggrbbb
......
......
......
......
......
.....h
oo...h""".toTsuField
    fieldW =
      """
bbbrry
yggbgy
ygbbgg
yrbyyy
bbrggy
~~~~~~
ggrbbb
......
......
......
......
......
.....h
oo...h""".toWaterField

  check fieldT.conn2 ==
    """
...rry
.....y
......
......
bbrgg.
ggr...
......
......
......
......
......
......
......""".toTsuField
  check fieldW.conn2 ==
    """
...rry
.....y
......
......
bbrgg.
~~~~~~
ggr...
......
......
......
......
......
......
......""".toWaterField

  check fieldT.conn2Vertical ==
    """
.....y
.....y
......
......
..r...
..r...
......
......
......
......
......
......
......""".toTsuField
  check fieldW.conn2Vertical ==
    """
.....y
.....y
......
......
..r...
~~~~~~
..r...
......
......
......
......
......
......
......""".toWaterField

  check fieldT.conn2Horizontal ==
    """
...rr.
......
......
......
bb.gg.
gg....
......
......
......
......
......
......
......""".toTsuField
  check fieldW.conn2Horizontal ==
    """
...rr.
......
......
......
bb.gg.
~~~~~~
gg....
......
......
......
......
......
......
......""".toWaterField

# ------------------------------------------------
# Connect - 3
# ------------------------------------------------

block: # conn3, conn3Vertical, conn3Horizontal, conn3LShape
  let
    fieldT =
      """
bbbrry
yggbgy
ygbbgg
yrbyyy
bbrggy
grrbbb
......
......
......
......
.....h
.....h
ooo..h""".toTsuField
    fieldW =
      """
bbbrry
yggbgy
ygbbgg
yrbyyy
bbrggy
~~~~~~
grrbbb
......
......
......
......
.....h
.....h
ooo..h""".toWaterField

  check fieldT.conn3 ==
    """
bbb...
ygg.g.
yg..gg
y.....
..r...
.rrbbb
......
......
......
......
......
......
......""".toTsuField
  check fieldW.conn3 ==
    """
bbb...
ygg.g.
yg..gg
y.....
..r...
~~~~~~
.rrbbb
......
......
......
......
......
......
......""".toWaterField

  check fieldT.conn3Vertical ==
    """
......
y.....
y.....
y.....
......
......
......
......
......
......
......
......
......""".toTsuField
  check fieldW.conn3Vertical ==
    """
......
y.....
y.....
y.....
......
~~~~~~
......
......
......
......
......
......
......
......""".toWaterField

  check fieldT.conn3Horizontal ==
    """
bbb...
......
......
......
......
...bbb
......
......
......
......
......
......
......""".toTsuField
  check fieldW.conn3Horizontal ==
    """
bbb...
......
......
......
......
~~~~~~
...bbb
......
......
......
......
......
......
......""".toWaterField

  check fieldT.conn3LShape ==
    """
......
.gg.g.
.g..gg
......
..r...
.rr...
......
......
......
......
......
......
......""".toTsuField
  check fieldW.conn3LShape ==
    """
......
.gg.g.
.g..gg
......
..r...
~~~~~~
.rr...
......
......
......
......
......
......
......""".toWaterField

# ------------------------------------------------
# Shift
# ------------------------------------------------

block: # shiftUp, shiftedDown, shiftRight, shiftLeft
  let
    fieldT =
      """
g.....
.y....
..p...
......
......
.....b
....r.
...o..
..h...
......
......
.....o
bg...p""".toTsuField
    fieldW =
      """
g.....
.y....
..p...
......
......
~~~~~~
.....b
....r.
...o..
..h...
......
......
.....o
bg...p""".toWaterField

  check fieldT.dup(shiftUp) ==
    """
.y....
..p...
......
......
.....b
....r.
...o..
..h...
......
......
.....o
bg...p
......""".toTsuField
  check fieldW.dup(shiftUp) ==
    """
.y....
..p...
......
......
.....b
~~~~~~
....r.
...o..
..h...
......
......
.....o
bg...p
......""".toWaterField

  check fieldT.dup(shiftDown) ==
    """
......
g.....
.y....
..p...
......
......
.....b
....r.
...o..
..h...
......
......
.....o""".toTsuField
  check fieldW.dup(shiftDown) ==
    """
......
g.....
.y....
..p...
......
~~~~~~
......
.....b
....r.
...o..
..h...
......
......
.....o""".toWaterField

  check fieldT.dup(shiftRight) ==
    """
.g....
..y...
...p..
......
......
......
.....r
....o.
...h..
......
......
......
.bg...""".toTsuField
  check fieldW.dup(shiftRight) ==
    """
.g....
..y...
...p..
......
......
~~~~~~
......
.....r
....o.
...h..
......
......
......
.bg...""".toWaterField

  check fieldT.dup(shiftLeft) ==
    """
......
y.....
.p....
......
......
....b.
...r..
..o...
.h....
......
......
....o.
g...p.""".toTsuField
  check fieldW.dup(shiftLeft) ==
    """
......
y.....
.p....
......
......
~~~~~~
....b.
...r..
..o...
.h....
......
......
....o.
g...p.""".toWaterField

# ------------------------------------------------
# Flip
# ------------------------------------------------

block: # flipVertical, flipHorizontal
  let
    fieldT =
      """
p.....
..o...
......
.h....
......
......
......
......
......
......
......
......
......""".toTsuField
    fieldW =
      """
p.....
..o...
......
.h....
......
~~~~~~
......
......
......
......
......
......
......
......""".toWaterField

  check fieldT.dup(flipVertical) ==
    """
......
......
......
......
......
......
......
......
......
.h....
......
..o...
p.....""".toTsuField
  check fieldW.dup(flipVertical) ==
    """
......
......
......
......
......
~~~~~~
......
......
......
......
.h....
......
..o...
p.....""".toWaterField

  check fieldT.dup(flipHorizontal) ==
    """
.....p
...o..
......
....h.
......
......
......
......
......
......
......
......
......""".toTsuField
  check fieldW.dup(flipHorizontal) ==
    """
.....p
...o..
......
....h.
......
~~~~~~
......
......
......
......
......
......
......
......""".toWaterField

# ------------------------------------------------
# Rotate
# ------------------------------------------------

block: # rotate, crossRotate
  let
    fieldT =
      """
p.....
..o...
......
...h..
......
......
......
......
......
......
......
......
....b.""".toTsuField
    fieldW =
      """
p.....
..o...
......
...h..
......
~~~~~~
......
......
......
......
......
......
......
....b.""".toWaterField

  check fieldT.dup(rotate) ==
    """
......
.b....
......
......
......
......
......
......
......
......
..h...
......
...o..""".toTsuField
  check fieldW.dup(rotate) ==
    """
......
.b....
......
......
......
~~~~~~
......
......
......
......
......
..h...
......
...o..""".toWaterField

  check fieldT.dup(crossRotate) ==
    """
......
....b.
......
......
......
......
......
......
......
......
.....h
......
o.....""".toTsuField
  check fieldW.dup(crossRotate) ==
    """
......
....b.
......
......
......
~~~~~~
......
......
......
......
......
.....h
......
o.....""".toWaterField

# ------------------------------------------------
# Pop
# ------------------------------------------------

block: # pop, canPop
  var
    fieldT =
      """
yo...g
yo..gg
yo...g
yy..oo
oo..oo
....rr
.r....
pp..h.
.p.ph.
..pph.
..phh.
.hhh..
......""".toTsuField
    fieldW =
      """
yo...g
yo..gg
yo...g
yy..oo
oo..oo
~~~~~~
....rr
.r....
pp..h.
.p.ph.
..pph.
..phh.
.hhh..
......""".toWaterField

  check fieldT.canPop
  check fieldW.canPop

  let
    resT = fieldT.pop
    resW = fieldW.pop

    yellow =
      """
......
x.....
x.....
xx....
......
......
......
......
......
......
......
......
......""".toBinField
    purple =
      """
......
......
......
......
......
......
......
......
...x..
..xx..
..x...
......
......""".toBinField
    hard =
      """
......
......
......
......
......
......
......
......
......
......
...x..
......
......""".toBinField
    hardToGarbage =
      """
......
......
......
......
......
......
......
......
....x.
....x.
......
..x...
......""".toBinField
    garbage =
      """
......
.x....
.x....
......
xx....
......
......
......
......
......
......
......
......""".toBinField
    zero = BinField.init
    popResAns = PopResult.init(
      zero, zero, zero, yellow, purple, hard, hardToGarbage, garbage, yellow + purple
    )

  check resT == popResAns
  check resW == popResAns

  check fieldT ==
    """
yo...g
....gg
.....g
....oo
....oo
....rr
.r....
pp..h.
.p..o.
....o.
....h.
.hoh..
......""".toTsuField
  check fieldW ==
    """
yo...g
....gg
.....g
....oo
....oo
~~~~~~
....rr
.r....
pp..h.
.p..o.
....o.
....h.
.hoh..
......""".toWaterField

  check not fieldT.canPop
  check not fieldW.canPop

# ------------------------------------------------
# Place
# ------------------------------------------------

block: # place
  block: # Tsu
    var field =
      """
.....o
....oo
....oo
....oo
....oo
....oo
....oo
....oo
....oo
....oo
....oo
....oo
...hoo""".toTsuField

    check field.dup(place(_, PurplePurple, NonePlacement)) == field

    field.place RedGreen, Right0
    check field ==
      """
.....o
....oo
....oo
....oo
....oo
....oo
....oo
....oo
....oo
....oo
....oo
....oo
rg.hoo""".toTsuField

    field.place BlueYellow, Down1
    check field ==
      """
.....o
....oo
....oo
....oo
....oo
....oo
....oo
....oo
....oo
....oo
.b..oo
.y..oo
rg.hoo""".toTsuField

    field.place PurpleRed, Left3
    check field ==
      """
.....o
....oo
....oo
....oo
....oo
....oo
....oo
....oo
....oo
....oo
.b..oo
.y.poo
rgrhoo""".toTsuField

    field.place GreenBlue, Up4
    check field ==
      """
....go
....oo
....oo
....oo
....oo
....oo
....oo
....oo
....oo
....oo
.b..oo
.y.poo
rgrhoo""".toTsuField

    field.place YellowPurple, OptPlacement.ok Down5
    check field ==
      """
....go
....oo
....oo
....oo
....oo
....oo
....oo
....oo
....oo
....oo
.b..oo
.y.poo
rgrhoo""".toTsuField

  block: # Water
    var field =
      """
......
......
......
......
......
~~~~~~
....h.
....o.
....o.
....o.
....o.
....o.
......
......""".toWaterField

    check field.dup(place(_, PurplePurple, NonePlacement)) == field

    field.place BlueYellow, Left4
    check field ==
      """
......
......
......
......
......
~~~~~~
...yb.
....h.
....o.
....o.
....o.
....o.
....o.
......""".toWaterField

    field.place PurpleRed, OptPlacement.ok Down4
    check field ==
      """
......
......
......
......
....p.
~~~~~~
...yr.
....b.
....h.
....o.
....o.
....o.
....o.
....o.""".toWaterField

# ------------------------------------------------
# Drop Garbages
# ------------------------------------------------

block: # dropGarbages
  block: # Tsu, garbage
    let field =
      """
....by
....by
..rgby
..rgby
..rgby
..rgby
..rgby
..rgby
..rgby
..rgby
..rgby
..rgby
..rgby""".toTsuField

    check field.dup(dropGarbages(_, [Col0: 0, 2, 0, 3, 0, 4], false)) ==
      """
...oby
...oby
..rgby
..rgby
..rgby
..rgby
..rgby
..rgby
..rgby
..rgby
..rgby
.orgby
.orgby""".toTsuField

  block: # Water, hard
    let field =
      """
......
......
......
......
......
~~~~~~
..pybg
..pybg
....bg
....bg
......
......
......
......""".toWaterField

    check field.dup(dropGarbages(_, [Col0: 0, 2, 0, 3, 5, 20], true)) ==
      """
.....h
.....h
.....h
.....h
....hh
~~~~~~
.hphhh
.hphhh
...hhh
...yhh
...ybg
....bg
....bg
....bg""".toWaterField

  block: # Water (full-water), garbage
    let field =
      """
....pr
....pr
....pr
..bypr
..bypr
~~~~~~
rgbypr
rgbypr
rgbypr
rgbypr
hhhhhh
rgbypr
oooooo
rgbypr""".toWaterField

    check field.dup(dropGarbages(_, [Col0: 0, 1, 0, 1, 0, 10], false)) ==
      """
....pr
....pr
...opr
..bypr
.obypr
~~~~~~
rgbypr
rgbypr
rgbypr
rgbypr
hhhhhh
rgbypr
oooooo
rgbypr""".toWaterField

# ------------------------------------------------
# Apply
# ------------------------------------------------

block: # apply
  let
    pair = RedGreen
    plcmt = Left3
    cnts = [Col0: 1, 2, 3, 4, 5, 6]
    dropHard = false

    step1 = Step.init(pair, plcmt)
    step2 = Step.init(cnts, dropHard)
    step3 = Step.init(cross = true)
    step4 = Step.init(cross = false)

  check TsuField.init.dup(apply(_, step1)) == TsuField.init.dup(place(_, pair, plcmt))
  check WaterField.init.dup(apply(_, step2)) ==
    WaterField.init.dup(dropGarbages(cnts, dropHard))

  let field = TsuField.init.dup(apply(_, step2))
  check field.dup(apply(_, step3)) == field.dup(crossRotate)
  check field.dup(apply(_, step4)) == field.dup(rotate)

# ------------------------------------------------
# Settle
# ------------------------------------------------

block: # settle, isSettled
  block: # Tsu
    var field =
      """
oo..o.
oo....
oo....
o.....
o.....
oo....
oo....
oo....
o.o...
oooh..
ooo...
ooo...
o.oo..""".toTsuField

    check not field.isSettled

    field.settle

    check field ==
      """
o.....
o.....
o.....
o.....
oo....
oo....
oo....
oo....
ooo...
ooo...
ooo...
oooh..
ooooo.""".toTsuField

    check field.isSettled

  block: # Water
    var field =
      """
....o.
....o.
...oo.
..h...
......
~~~~~~
o...o.
oh.oo.
......
.o.oo.
....o.
.o..o.
......
....o.""".toWaterField

    check not field.isSettled

    field.settle

    check field ==
      """
......
......
......
......
....o.
~~~~~~
ohhoo.
oo.oo.
.o.oo.
....o.
....o.
....o.
....o.
....o.""".toWaterField

    check field.isSettled

# ------------------------------------------------
# Move
# ------------------------------------------------

block: # move
  block: # Tsu
    let
      fieldBefore =
        """
....g.
....g.
....pg
....rg
....gr
....go
....gp
....bg
....bp
...bgp
...bgr
...orb
...gbb""".toTsuField
      fieldAfter =
        """
......
......
......
......
......
......
......
......
......
......
.....r
.....b
...gbb""".toTsuField

      pair = RedBlue
      plcmt = Down3

      chainCnt = 3
      popCnts = [None: 0, 0, 2, 4, 10, 5, 0, 4]
      hardToGarbageCnt = 0
      detailHardToGarbageCnt = @[0, 0, 0]

      garbagesCnt = [Col0: 0, 1, 2, 0, 2, 1]

      detailArr1: array[Cell, int] = [0, 0, 1, 0, 0, 5, 0, 0]
      detailArr2: array[Cell, int] = [0, 0, 0, 0, 10, 0, 0, 0]
      detailArr3: array[Cell, int] = [0, 0, 1, 4, 0, 0, 0, 4]
      detailPopCnts = @[detailArr1, detailArr2, detailArr3]

      fullArr1: array[Cell, seq[int]] = [@[], @[], @[], @[], @[], @[5], @[], @[]]
      fullArr2: array[Cell, seq[int]] = [@[], @[], @[], @[], @[4, 6], @[], @[], @[]]
      fullArr3: array[Cell, seq[int]] = [@[], @[], @[], @[4], @[], @[], @[], @[4]]
      fullPopCnts = @[fullArr1, fullArr2, fullArr3]

    block: # calc conn
      var field2 = fieldBefore
      check field2.move(Step.init(pair, plcmt)) ==
        MoveResult.init(
          chainCnt, popCnts, hardToGarbageCnt, detailPopCnts, detailHardToGarbageCnt,
          fullPopCnts,
        )
      check field2 == fieldAfter

    block: # not calcConn
      var field2 = fieldBefore
      check field2.move(pair, plcmt, false) ==
        MoveResult.init(
          chainCnt, popCnts, hardToGarbageCnt, detailPopCnts, detailHardToGarbageCnt
        )
      check field2 == fieldAfter

    block: # drop garbage
      var field2 = fieldBefore
      check field2.move(garbagesCnt, false, false) ==
        MoveResult.init(0, initArrWith[Cell, int](0), 0, @[], @[])
      check field2 ==
        """
....g.
....go
....pg
....rg
....gr
....go
....gp
....bg
....bp
...bgp
...bgr
..oorb
.oogbb""".toTsuField

    block: # drop hard
      var field2 = fieldBefore
      check field2.move(Step.init(garbagesCnt, true), false) ==
        MoveResult.init(0, initArrWith[Cell, int](0), 0, @[], @[])
      check field2 ==
        """
....g.
....gh
....pg
....rg
....gr
....go
....gp
....bg
....bp
...bgp
...bgr
..horb
.hhgbb""".toTsuField

    block: # rotate
      let
        fieldBefore2 =
          """
......
......
......
......
......
......
......
......
......
..r...
.ro...
.ro...
.roo..""".toTsuField
        fieldAfter2 =
          """
......
......
......
......
......
......
......
......
......
......
......
......
...o..""".toTsuField
      var field2 = fieldBefore2
      let popCnts: array[Cell, int] = [0, 0, 3, 4, 0, 0, 0, 0]
      check field2.move(Step.init(cross = false), false) ==
        MoveResult.init(1, popCnts, 0, @[popCnts], @[0])
      check field2 == fieldAfter2

# ------------------------------------------------
# Field <-> array
# ------------------------------------------------

block: # toArr, toTsuField, toWaterField
  let
    fieldT =
      """
r.....
.g....
..b...
...y..
....p.
.....o
....h.
......
......
......
......
......
......""".toTsuField
    fieldW =
      """
r.....
.g....
..b...
...y..
....p.
~~~~~~
.....o
....h.
......
......
......
......
......
......""".toWaterField

    arrT = fieldT.toArr
    arrW = fieldW.toArr

    arrAns = [
      [Red, None, None, None, None, None],
      [None, Green, None, None, None, None],
      [None, None, Blue, None, None, None],
      [None, None, None, Yellow, None, None],
      [None, None, None, None, Purple, None],
      [None, None, None, None, None, Garbage],
      [None, None, None, None, Hard, None],
      [None, None, None, None, None, None],
      [None, None, None, None, None, None],
      [None, None, None, None, None, None],
      [None, None, None, None, None, None],
      [None, None, None, None, None, None],
      [None, None, None, None, None, None],
    ]

  check arrT == arrAns
  check arrW == arrAns

  check arrT.toTsuField == fieldT
  check arrW.toWaterField == fieldW

# ------------------------------------------------
# Field <-> string
# ------------------------------------------------

block: # `$`, parseTsuField, parseWaterField
  let
    strT =
      """
r.....
.g....
..b...
...y..
....p.
.....o
....h.
......
......
......
......
......
......"""
    strW =
      """
r.....
.g....
..b...
...y..
....p.
~~~~~~
.....o
....h.
......
......
......
......
......
......"""

    fieldT = strT.toTsuField
    fieldW = strW.toWaterField

  check $fieldT == strT
  check $fieldW == strW

  check strT.parseTsuField == Res[TsuField].ok fieldT
  check strW.parseWaterField == Res[WaterField].ok fieldW

  check strT.parseWaterField.isErr
  check strW.parseTsuField.isErr

# ------------------------------------------------
# Field <-> URI
# ------------------------------------------------

block: # toUruQuery, parseTsuField, parseWaterField
  let
    fieldT =
      """
......
......
......
......
......
r.....
.g....
..b...
...y..
....p.
.....o
....h.
......""".toTsuField
    fieldW =
      """
......
......
......
......
r.....
~~~~~~
.g....
..b...
...y..
....p.
.....o
....h.
......
......""".toWaterField

  block: # Pon2
    let
      queryTRes = fieldT.toUriQuery Pon2
      queryWRes = fieldW.toUriQuery Pon2

    check queryTRes ==
      Res[string].ok "t_r......g......b......y......p......o....h......."
    check queryWRes == Res[string].ok "w_r.....~.g......b......y......p......o....h"

    let
      queryT = queryTRes.unsafeValue
      queryW = queryWRes.unsafeValue

    check queryT.parseTsuField(Pon2) == Res[TsuField].ok fieldT
    check queryW.parseWaterField(Pon2) == Res[WaterField].ok fieldW

    check queryW.parseTsuField(Pon2).isErr
    check queryT.parseWaterField(Pon2).isErr

  block: # Ishikawa, Ips
    for fqdn in [Ishikawa, Ips]:
      let
        queryTRes = fieldT.toUriQuery fqdn
        queryWRes = fieldW.toUriQuery fqdn

      check queryTRes == Res[string].ok "~1.02.003.0004.00005.00000600009."
      check queryWRes.isErr

      let queryT = queryTRes.unsafeValue

      check queryT.parseTsuField(fqdn) == Res[TsuField].ok fieldT

      check "t-r".parseTsuField(fqdn).isErr

  block: # Ishikawa, Ips (not tilde)
    let field =
      """
......
......
......
......
......
......
......
......
......
......
......
......
.r..g.""".toTsuField

    for fqdn in [Ishikawa, Ips]:
      let queryRes = field.toUriQuery(fqdn)
      check queryRes == Res[string].ok "10g"
      check queryRes.unsafeValue.parseTsuField(fqdn) == Res[TsuField].ok field

  block: # empty field
    check TsuField.init.toUriQuery(Pon2) == Res[string].ok "t_"
    check TsuField.init.toUriQuery(Ishikawa) == Res[string].ok ""
    check TsuField.init.toUriQuery(Ips) == Res[string].ok ""
    check WaterField.init.toUriQuery(Pon2) == Res[string].ok "w_~"

  block: # empty query
    check "".parseTsuField(Pon2) == Res[TsuField].ok TsuField.init
    check "".parseWaterField(Pon2).isErr
