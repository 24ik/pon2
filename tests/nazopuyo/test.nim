{.push raises: [].}
{.experimental: "strictDefs".}
{.experimental: "strictFuncs".}
{.experimental: "views".}

import std/[unittest]
import
  ../../src/pon2/core/
    [field, fqdn, goal, nazopuyo, placement, popresult, puyopuyo, rule, step]
import ../../src/pon2/private/[assign, strutils]

func mark2(nazoPuyo: NazoPuyo, placements: varargs[Placement]): MarkResult =
  var nazoPuyo2 = nazoPuyo
  for i, placement in placements:
    nazoPuyo2.puyoPuyo.steps[i].placement.assign placement

  nazoPuyo2.mark

# ------------------------------------------------
# Constructor
# ------------------------------------------------

block: # init
  check NazoPuyo.init(PuyoPuyo.init, Goal.init) ==
    NazoPuyo(puyoPuyo: PuyoPuyo.init, goal: Goal.init)
  check NazoPuyo.init == NazoPuyo.init(PuyoPuyo.init, Goal.init)
  check NazoPuyo.init(Rule.Spinner) ==
    NazoPuyo.init(PuyoPuyo.init Rule.Spinner, Goal.init)

# ------------------------------------------------
# Mark
# ------------------------------------------------

block: # mark
  block: # Chain
    let nazoPuyo =
      """
ちょうど3連鎖するべし
======
[すいちゅう]
......
......
......
......
......
~~~~~~
rpor..
.pp...
..y...
..y...
..y...
......
......
......
------
ry|
rp|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Left2, Down1) == Correct
    check nazoPuyo.mark2(Down2, Down1) == Incorrect

  block: # Color
    let nazoPuyo =
      """
ちょうど2色同時に消すべし
======
[通]
......
......
......
......
......
......
...g..
...po.
...pg.
...pg.
...og.
.o.pp.
.gggp.
------
gp|
gp|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Left5, Down5) == Correct
    check nazoPuyo.mark2(Down5, Up4) == Incorrect

  block: # Count
    let nazoPuyo =
      """
ぷよ12個以上同時に消すべし
======
[だいかいてん]
......
......
......
......
......
......
......
......
......
...r..
..rr..
rggb..
rrgbb.
------
rb|
rg|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Down2, Left2) == Correct
    check nazoPuyo.mark2(Down5, Down1) == Incorrect

  block: # Place
    let nazoPuyo =
      """
ぷよちょうど2箇所で同時に消すべし
======
[クロスかいてん]
......
......
......
......
......
......
......
......
......
......
....r.
y...y.
yyryy.
------
ry|
ry|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Down3, Left2) == Correct
    check nazoPuyo.mark2(Up2, Down3) == Incorrect

  block: # Connection
    let nazoPuyo =
      """
ぷよ7連結以上で消すべし
======
[通]
......
......
......
......
......
......
......
......
..o...
grgr..
ggrg..
rrbb..
gggbr.
------
gg|
bg|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Up3, Left4) == Correct
    check nazoPuyo.mark2(Up1, Left2) == Incorrect

  block: # AccumColor
    let nazoPuyo =
      """
累計ちょうど3色消すべし
======
[だいかいてん]
......
......
......
......
......
......
......
......
......
......
......
...gbr
.bgbrr
------
bg|
rg|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Down2, Left5) == Correct
    check nazoPuyo.mark2(Left3, Left5) == Incorrect

  block: # AccumCount
    let nazoPuyo =
      """
ぷよ累計18個以上消すべし
======
[クロスかいてん]
......
......
......
......
......
......
......
......
..b...
..y...
ggg...
ooo...
ooobbb
------
yy|
yg|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Up0, Down1) == Correct
    check nazoPuyo.mark2(Right3, Down3) == Incorrect

  block: # Clear
    let nazoPuyo =
      """
ぷよ全て消すべし
======
[通]
......
......
......
......
......
......
......
......
......
......
......
bb.yyy
yy.bbb
------
by|
by|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Left3, Down2) == Correct
    check nazoPuyo.mark2(Left3, Placement.None) == Incorrect

  block: # Clear w/ kind
    let nazoPuyo =
      """
2色以上同時に消す&ぷよ全て消すべし
======
[だいかいてん]
......
......
......
......
......
......
......
......
......
......
o..g..
or.g..
orrg..
------
rg|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Right1) == Correct
    check nazoPuyo.mark2(Up2) == Incorrect

  block: # Dead, InvalidPlace, PlaceSkip
    let nazoPuyo =
      """
ぷよ全て消すべし
======
[クロスかいてん]
......
.....o
.....o
..o..o
..o..o
..o..o
..o..o
..o..o
..o..o
..o..o
..o..o
..o..o
..o..o
------
rg|
by|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Up2) == Dead
    check nazoPuyo.mark2(Up5) == InvalidPlace
    check nazoPuyo.mark2(Placement.None, Up3) == PlaceSkip

  block: # NotSupport
    let nazoPuyo =
      """
おじゃまぷよ2箇所以上で同時に消すべし
======
[通]
......
......
......
......
......
......
......
......
......
......
....r.
y...y.
yyryy.
------
ry|
ry|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Placement.None) == NotSupport

  block: # initial nazopuyo is Corrected
    let nazoPuyo =
      """
ぷよ全て消すべし
======
[だいかいてん]
......
......
......
......
......
......
......
......
......
......
......
......
......
------
rr|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Down1) == Incorrect

  block: # empty steps
    let nazoPuyo =
      """
ぷよ全て消すべし
======
[クロスかいてん]
......
......
......
......
......
......
......
......
......
......
......
......
rrr...
------
""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2 == Incorrect

  block: # w/ endStepIndex
    let nazoPuyo =
      """
ぷよ全て消すべし
======
[通]
......
......
......
......
......
......
......
......
......
......
......
bb.yyy
yy.bbb
------
by|43
by|3S""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark(-1) == Correct
    check nazoPuyo.mark(0) == Incorrect
    check nazoPuyo.mark(1) == Incorrect
    check nazoPuyo.mark(2) == Correct

# ------------------------------------------------
# Nazo Puyo <-> string
# ------------------------------------------------

block: # `$`, parseNazoPuyo
  let
    str =
      """
色ぷよ2連結以上で消すべし
======
[だいかいてん]
r.....
.g....
..b...
...y..
....p.
.....o
....h.
......
......
......
......
......
......
------
by|
(0,1,0,0,0,2)
rg|23
[3,0,0,0,4,0]
pp|4N"""

    nazoPuyo = str.parseNazoPuyo.unsafeValue

  check $nazoPuyo == str

# ------------------------------------------------
# Nazo Puyo <-> URI
# ------------------------------------------------

block: # toUriQuery, parseNazoPuyo
  block:
    let
      str =
        """
ちょうど2連鎖するべし
======
[通]
......
......
......
......
......
......
......
......
......
......
.op...
...yg.
...b.r
------
by|
(0,1,0,0,0,1)
rg|23"""
      nazoPuyo = str.parseNazoPuyo.unsafeValue

      queryPon2 = "field=0_op......yg....b.r&steps=byo0_1_0_0_0_1org23&goal=0_0_2_0_"
      queryIshikawa = "6E004g031_E1ahce__u02"

    check nazoPuyo.toUriQuery(Pon2) == Pon2Result[string].ok queryPon2
    check nazoPuyo.toUriQuery(IshikawaPuyo) == Pon2Result[string].ok queryIshikawa
    check nazoPuyo.toUriQuery(Ips) == Pon2Result[string].ok queryIshikawa

    check queryPon2.parseNazoPuyo(Pon2) == Pon2Result[NazoPuyo].ok nazoPuyo
    check queryIshikawa.parseNazoPuyo(IshikawaPuyo) == Pon2Result[NazoPuyo].ok nazoPuyo
    check queryIshikawa.parseNazoPuyo(Ips) == Pon2Result[NazoPuyo].ok nazoPuyo

  block: # empty steps
    let
      str =
        """
赤ぷよ全て消すべし
======
[通]
......
......
......
......
......
......
......
......
......
......
......
......
g.....
------
"""
      nazoPuyo = str.parseNazoPuyo.unsafeValue

      queryPon2 = "field=0_g.....&steps&goal=_3"
      queryIshikawa = "g00___210"

    check nazoPuyo.toUriQuery(Pon2) == Pon2Result[string].ok queryPon2
    check nazoPuyo.toUriQuery(IshikawaPuyo) == Pon2Result[string].ok queryIshikawa
    check nazoPuyo.toUriQuery(Ips) == Pon2Result[string].ok queryIshikawa

    check queryPon2.parseNazoPuyo(Pon2) == Pon2Result[NazoPuyo].ok nazoPuyo
    check queryIshikawa.parseNazoPuyo(IshikawaPuyo) == Pon2Result[NazoPuyo].ok nazoPuyo
    check queryIshikawa.parseNazoPuyo(Ips) == Pon2Result[NazoPuyo].ok nazoPuyo

  block: # empty field and steps
    let
      str =
        """
ちょうど3色同時に消すべし
======
[通]
......
......
......
......
......
......
......
......
......
......
......
......
......
------
"""
      nazoPuyo = str.parseNazoPuyo.unsafeValue

      queryPon2 = "field=0_&steps&goal=1_0_3_0_"
      queryIshikawa = "___E03"

    check nazoPuyo.toUriQuery(Pon2) == Pon2Result[string].ok queryPon2
    check nazoPuyo.toUriQuery(IshikawaPuyo) == Pon2Result[string].ok queryIshikawa
    check nazoPuyo.toUriQuery(Ips) == Pon2Result[string].ok queryIshikawa

    check queryPon2.parseNazoPuyo(Pon2) == Pon2Result[NazoPuyo].ok nazoPuyo
    check queryIshikawa.parseNazoPuyo(IshikawaPuyo) == Pon2Result[NazoPuyo].ok nazoPuyo
    check queryIshikawa.parseNazoPuyo(Ips) == Pon2Result[NazoPuyo].ok nazoPuyo

  block: # empty query
    let
      str =
        """
クリア条件未設定
======
[通]
......
......
......
......
......
......
......
......
......
......
......
......
......
------
"""
      nazoPuyo = str.parseNazoPuyo.unsafeValue

      queryPon2 = "field=0_&steps&goal=_"
      queryPon22 = "field=0_&steps&goal="
      queryPon23 = "field=0_&steps"

    check nazoPuyo.toUriQuery(Pon2) == Pon2Result[string].ok queryPon2

    for query in [queryPon2, queryPon22, queryPon23]:
      check query.parseNazoPuyo(Pon2) == Pon2Result[NazoPuyo].ok nazoPuyo
