{.push raises: [].}
{.experimental: "strictDefs".}
{.experimental: "strictFuncs".}
{.experimental: "views".}

import std/[unittest]
import
  ../../src/pon2/core/
    [field, fqdn, goal, nazopuyo, placement, popresult, puyopuyo, rule, step]
import ../../src/pon2/private/[strutils]

func mark2(nazoPuyo: NazoPuyo, placements: varargs[Placement]): MarkResult =
  var nazoPuyo2 = nazoPuyo
  for i, placement in placements:
    nazoPuyo2.puyoPuyo.steps[i].optPlacement.ok placement

  nazoPuyo2.mark

func mark2(nazoPuyo: NazoPuyo, optPlacements: varargs[OptPlacement]): MarkResult =
  var nazoPuyo2 = nazoPuyo
  for i, optPlacement in optPlacements:
    nazoPuyo2.puyoPuyo.steps[i].optPlacement = optPlacement

  nazoPuyo2.mark

# ------------------------------------------------
# Constructor
# ------------------------------------------------

block: # init
  let
    puyoPuyoT = PuyoPuyo.init
    puyoPuyoW = PuyoPuyo.init Rule.Water
    goal = Goal.init(Count, Colors, 10, Exact)
  check NazoPuyo.init(puyoPuyoT, goal) == NazoPuyo(puyoPuyo: puyoPuyoT, goal: goal)
  check NazoPuyo.init(puyoPuyoW, goal) == NazoPuyo(puyoPuyo: puyoPuyoW, goal: goal)

  check NazoPuyo.init(Spinner) == NazoPuyo.init(PuyoPuyo.init Spinner, Goal.init)
  check NazoPuyo.init == NazoPuyo.init Rule.Tsu

# ------------------------------------------------
# Mark
# ------------------------------------------------

block: # mark
  block: # Chain
    let nazoPuyo =
      """
ちょうど3連鎖するべし
======
[すいちゅう]
......
......
......
......
......
~~~~~~
rpor..
.pp...
..y...
..y...
..y...
......
......
......
------
ry|
rp|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Left2, Down1) == Accept
    check nazoPuyo.mark2(Down2, Down1) == WrongAnswer

  block: # Color
    let nazoPuyo =
      """
ちょうど2色同時に消すべし
======
[通]
......
......
......
......
......
......
...g..
...po.
...pg.
...pg.
...og.
.o.pp.
.gggp.
------
gp|
gp|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Left5, Down5) == Accept
    check nazoPuyo.mark2(Down5, Up4) == WrongAnswer

  block: # Count
    let nazoPuyo =
      """
ぷよ12個以上同時に消すべし
======
[だいかいてん]
......
......
......
......
......
......
......
......
......
...r..
..rr..
rggb..
rrgbb.
------
rb|
rg|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Down2, Left2) == Accept
    check nazoPuyo.mark2(Down5, Down1) == WrongAnswer

  block: # Place
    let nazoPuyo =
      """
ぷよちょうど2箇所で同時に消すべし
======
[クロスかいてん]
......
......
......
......
......
......
......
......
......
......
....r.
y...y.
yyryy.
------
ry|
ry|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Down3, Left2) == Accept
    check nazoPuyo.mark2(Up2, Down3) == WrongAnswer

  block: # Connection
    let nazoPuyo =
      """
ぷよ7連結以上で消すべし
======
[通]
......
......
......
......
......
......
......
......
..o...
grgr..
ggrg..
rrbb..
gggbr.
------
gg|
bg|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Up3, Left4) == Accept
    check nazoPuyo.mark2(Up1, Left2) == WrongAnswer

  block: # AccumColor
    let nazoPuyo =
      """
累計ちょうど3色消すべし
======
[だいかいてん]
......
......
......
......
......
......
......
......
......
......
......
...gbr
.bgbrr
------
bg|
rg|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Down2, Left5) == Accept
    check nazoPuyo.mark2(Left3, Left5) == WrongAnswer

  block: # AccumCount
    let nazoPuyo =
      """
ぷよ累計18個以上消すべし
======
[クロスかいてん]
......
......
......
......
......
......
......
......
..b...
..y...
ggg...
ooo...
ooobbb
------
yy|
yg|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Up0, Down1) == Accept
    check nazoPuyo.mark2(Right3, Down3) == WrongAnswer

  block: # Clear
    let nazoPuyo =
      """
ぷよ全て消すべし
======
[通]
......
......
......
......
......
......
......
......
......
......
......
bb.yyy
yy.bbb
------
by|
by|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Left3, Down2) == Accept
    check nazoPuyo.mark2(OptPlacement.ok Left3, NonePlacement) == WrongAnswer

  block: # Clear w/ kind
    let nazoPuyo =
      """
2色以上同時に消す&ぷよ全て消すべし
======
[だいかいてん]
......
......
......
......
......
......
......
......
......
......
o..g..
or.g..
orrg..
------
rg|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Right1) == Accept
    check nazoPuyo.mark2(Up2) == WrongAnswer

  block: # Dead, InvalidMove, SkipMove
    let nazoPuyo =
      """
ぷよ全て消すべし
======
[クロスかいてん]
......
.....o
.....o
..o..o
..o..o
..o..o
..o..o
..o..o
..o..o
..o..o
..o..o
..o..o
..o..o
------
rg|
by|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Up2) == Dead
    check nazoPuyo.mark2(Up5) == InvalidMove
    check nazoPuyo.mark2(NonePlacement, OptPlacement.ok Up3) == SkipMove

  block: # NotSupport
    let nazoPuyo =
      """
おじゃまぷよ2箇所以上で同時に消すべし
======
[通]
......
......
......
......
......
......
......
......
......
......
....r.
y...y.
yyryy.
------
ry|
ry|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(NonePlacement) == NotSupport

  block: # initial nazopuyo is accepted
    let nazoPuyo =
      """
ぷよ全て消すべし
======
[だいかいてん]
......
......
......
......
......
......
......
......
......
......
......
......
......
------
rr|""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2(Down1) == WrongAnswer

  block: # empty steps
    let nazoPuyo =
      """
ぷよ全て消すべし
======
[クロスかいてん]
......
......
......
......
......
......
......
......
......
......
......
......
rrr...
------
""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark2 == WrongAnswer

  block: # w/ endStepIndex
    let nazoPuyo =
      """
ぷよ全て消すべし
======
[通]
......
......
......
......
......
......
......
......
......
......
......
bb.yyy
yy.bbb
------
by|43
by|3S""".parseNazoPuyo.unsafeValue

    check nazoPuyo.mark(-1) == Accept
    check nazoPuyo.mark(0) == WrongAnswer
    check nazoPuyo.mark(1) == WrongAnswer
    check nazoPuyo.mark(2) == Accept

# ------------------------------------------------
# Nazo Puyo <-> string
# ------------------------------------------------

block: # `$`, parseNazoPuyo
  let
    str =
      """
色ぷよ2連結以上で消すべし
======
[だいかいてん]
r.....
.g....
..b...
...y..
....p.
.....o
....h.
......
......
......
......
......
......
------
by|
(0,1,0,0,0,2)
rg|23
[3,0,0,0,4,0]
pp|4N"""

    nazoPuyo = str.parseNazoPuyo.unsafeValue

  check $nazoPuyo == str

# ------------------------------------------------
# Nazo Puyo <-> URI
# ------------------------------------------------

block: # toUriQuery, parseNazoPuyo
  block:
    let
      str =
        """
ちょうど2連鎖するべし
======
[通]
......
......
......
......
......
......
......
......
......
......
.op...
...yg.
...b.r
------
by|
(0,1,0,0,0,1)
rg|23"""
      nazoPuyo = str.parseNazoPuyo.unsafeValue

      queryPon2 = "field=0_op......yg....b.r&steps=byo0_1_0_0_0_1org23&goal=0_0_2_0_"
      queryIshikawa = "6E004g031_E1ahce__u02"

    check nazoPuyo.toUriQuery(Pon2) == Pon2Result[string].ok queryPon2
    check nazoPuyo.toUriQuery(Ishikawa) == Pon2Result[string].ok queryIshikawa
    check nazoPuyo.toUriQuery(Ips) == Pon2Result[string].ok queryIshikawa

    check queryPon2.parseNazoPuyo(Pon2) == Pon2Result[NazoPuyo].ok nazoPuyo
    check queryIshikawa.parseNazoPuyo(Ishikawa) == Pon2Result[NazoPuyo].ok nazoPuyo
    check queryIshikawa.parseNazoPuyo(Ips) == Pon2Result[NazoPuyo].ok nazoPuyo

  block: # empty steps
    let
      str =
        """
赤ぷよ全て消すべし
======
[通]
......
......
......
......
......
......
......
......
......
......
......
......
g.....
------
"""
      nazoPuyo = str.parseNazoPuyo.unsafeValue

      queryPon2 = "field=0_g.....&steps&goal=_1"
      queryIshikawa = "g00___210"

    check nazoPuyo.toUriQuery(Pon2) == Pon2Result[string].ok queryPon2
    check nazoPuyo.toUriQuery(Ishikawa) == Pon2Result[string].ok queryIshikawa
    check nazoPuyo.toUriQuery(Ips) == Pon2Result[string].ok queryIshikawa

    check queryPon2.parseNazoPuyo(Pon2) == Pon2Result[NazoPuyo].ok nazoPuyo
    check queryIshikawa.parseNazoPuyo(Ishikawa) == Pon2Result[NazoPuyo].ok nazoPuyo
    check queryIshikawa.parseNazoPuyo(Ips) == Pon2Result[NazoPuyo].ok nazoPuyo

  block: # empty field and steps
    let
      str =
        """
ちょうど3色同時に消すべし
======
[通]
......
......
......
......
......
......
......
......
......
......
......
......
......
------
"""
      nazoPuyo = str.parseNazoPuyo.unsafeValue

      queryPon2 = "field=0_&steps&goal=1_0_3_0_"
      queryIshikawa = "___E03"

    check nazoPuyo.toUriQuery(Pon2) == Pon2Result[string].ok queryPon2
    check nazoPuyo.toUriQuery(Ishikawa) == Pon2Result[string].ok queryIshikawa
    check nazoPuyo.toUriQuery(Ips) == Pon2Result[string].ok queryIshikawa

    check queryPon2.parseNazoPuyo(Pon2) == Pon2Result[NazoPuyo].ok nazoPuyo
    check queryIshikawa.parseNazoPuyo(Ishikawa) == Pon2Result[NazoPuyo].ok nazoPuyo
    check queryIshikawa.parseNazoPuyo(Ips) == Pon2Result[NazoPuyo].ok nazoPuyo

  block: # empty query
    let
      str =
        """
クリア条件未設定
======
[通]
......
......
......
......
......
......
......
......
......
......
......
......
......
------
"""
      nazoPuyo = str.parseNazoPuyo.unsafeValue

      queryPon2 = "field=0_&steps&goal=_"
      queryPon22 = "field=0_&steps&goal="
      queryPon23 = "field=0_&steps"

    check nazoPuyo.toUriQuery(Pon2) == Pon2Result[string].ok queryPon2

    for query in [queryPon2, queryPon22, queryPon23]:
      check query.parseNazoPuyo(Pon2) == Pon2Result[NazoPuyo].ok nazoPuyo
